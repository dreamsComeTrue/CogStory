bool g_IntroPlayed = false;

Point playerSize = player.GetSize ();
Point screenSize = screen.GetWindowSize ();

Actor@ lastActionActor = null;

string ChoiceFunction1 ()
{
    Log ("OK");

    return "SPCH_2";
}

void BeforeEnterScene ()
{
    SetSuppressSceneInfo (true);
    AudioStream@ stream = SetSceneAudioStream ("music/Caketown 1.ogg");
    stream.Play ();

    player.SetActionHandler (PlayerActionFunc);

    Actor@ npc = GetActor ("npc_girl");

    if (!g_IntroPlayed)
    {
        RegisterChoiceFunction ("choice1", ChoiceFunction1);

        g_IntroPlayed = true;
        IntroAnimation ();        
        npc.AddCollisionCallback (CollisionFunc);
    }
    else
    {
        camera.SetFollowActor (GetActor ("PLAYER"));
    }

    GetActor ("npc_x").AssignFlagPointsToWalk ("A");
    GetActor ("npc_girl").AssignFlagPointsToWalk ("NPC_1_A");

    RegisterSpeechesFinishedHandler (SpeechesFinished);
}

void PlayerActionFunc ()
{
    Actor@ npc = GetActor ("npc_girl");

    if (npc.IsOverlaping (player))
    {
        if (npc.GetTypeName () == "NPC")
        {
            @lastActionActor = @npc;

            npc.OrientTo (player);
            npc.SuspendUpdate ();
            SpeechFrame@ frame = AddSpeechFrame ("GREET_1", true);
            frame.SetHandleFunction (SpeechHandle);
        }
        else
        {
            Log ("Hello");
        }
    }
}

void CollisionFunc (Actor@ other)
{
    if (player.IsAction ())
    {
        if (other.GetTypeName () == "Player")
        {
            SpeechFrame@ frame = AddSpeechFrame ("GREET_1", true);
            frame.SetHandleFunction (SpeechHandle);
        }
        else
        {
            Log ("Hello");
        }
    }
}

void SpeechesFinished ()
{
    if (!(lastActionActor is null))
    {
        lastActionActor.ResumeUpdate ();
    }
}

void SpeechHandle ()
{
    Log ("OK");
}

void IntroAnimation ()
{
    Point screenSize = screen.GetWindowSize ();

    player.SetPreventInput (true);
    player.SetPosition (0, 0);

    camera.SetTranslate (screenSize.Width * 0.5f - player.Bounds.GetSize ().Width, -200);
	camera.SetFollowActor (null);
    camera.TweenToPoint (player.GetPosition () + player.Bounds.GetHalfSize (), IntroAnimationFinish, 5000, true);

    player.SetCurrentAnimation ("ANIM_MOVE_UP_LOOK");

    SetOverlayText ("-- Chapter I --\nNo ordinary day", 3000.f, 100.f, Center);    
}

void IntroAnimationFinish (int id)
{
    player.SetPreventInput (false);
}

void OnCityExitEnter (Point p)
{
//	Color color = COLOR_BLACK;
//	SceneFadeInOut (400, 400, color);
	SetActiveScene ("0_home/0_0_home.scn", true);
}

void Update (float deltaTime)
{
}
